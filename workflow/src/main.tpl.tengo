wf := import("@platforma-sdk/workflow-tengo:workflow")
assets:= import("@platforma-sdk/workflow-tengo:assets")
render := import("@platforma-sdk/workflow-tengo:render")
xsv := import("@platforma-sdk/workflow-tengo:pframes.xsv")
pframes := import("@platforma-sdk/workflow-tengo:pframes")
pSpec := import("@platforma-sdk/workflow-tengo:pframes.spec")

pfTopTableConv := import(":pf-top-table-conv")
pfDeErrorsConv := import(":pf-de-errors-conv")
pfDEGConv := import(":pf-DEG-conv")

runWilcoxonTpl := assets.importTemplate(":runWilcoxon")

wf.prepare(func(args){
	
	metaRefs := {}

	i := 0
	for metaRef in args.covariateRefs {
		metaRefs["metaRef" + i ] = wf.resolve(metaRef, { errIfMissing: true })
		i = i + 1
	}

	return {
		resolvedInput: wf.resolve(args.countsRef, { errIfMissing: true }),
		resolvedContrastFactor: wf.resolve(args.contrastFactor),
		metaRefs: metaRefs 
	}
})

wf.body(func(args) {

	blockId := wf.blockId().getDataAsJson()
	rawCounts := args.resolvedInput
	inputSpec := rawCounts.spec
	species := inputSpec.domain["pl7.app/species"]

	contrastFactor := args.resolvedContrastFactor
	contrastFactorName := contrastFactor.spec.annotations["pl7.app/label"]
	numerators := args.numerators
	denominator := args.denominator
	pAdjThreshold := args.pAdjThreshold
	log2FCThreshold := args.log2FCThreshold

	// Set default conversion memory and CPU
	defaultConvMem := "16GiB"
	defaultConvCpu := 1

	covariates := []
	for _, v in args.metaRefs {
		covariates = append(covariates, v)
	}

	csvCovariates := xsv.exportFrame(covariates, "csv", { mem: defaultConvMem, cpu: defaultConvCpu })
	csvCounts := xsv.exportFrame([rawCounts], "csv", { mem: defaultConvMem, cpu: defaultConvCpu })

	runWilcoxonRender := render.create(runWilcoxonTpl, {
		csvCounts: csvCounts,
		csvCovariates: csvCovariates,
		numerators: numerators,
		contrastFactorName: contrastFactorName,
		params: {
			denominator: denominator,
			pAdjThreshold: pAdjThreshold,
			log2FCThreshold: log2FCThreshold
		}
	})

	// Import CSV files as pFrames
	topTableImportParams := pfTopTableConv.getColumns(blockId, species, log2FCThreshold, pAdjThreshold)
	topTablePf := xsv.importFile(runWilcoxonRender.output("topTableCsv"), "csv", topTableImportParams, { mem: defaultConvMem, cpu: defaultConvCpu })
	topTableFilteredPf := xsv.importFile(runWilcoxonRender.output("topTableFilteredCsv"), "csv", topTableImportParams, { mem: defaultConvMem, cpu: defaultConvCpu })
	
	deErrorsImportParams := pfDeErrorsConv.getColumns()
	deErrorsPf := xsv.importFile(runWilcoxonRender.output("deErrorsCsv"), "csv", deErrorsImportParams, { mem: defaultConvMem, cpu: defaultConvCpu })

	DEGImportParams := pfDEGConv.getColumns(blockId, species)
	DEGPf := xsv.importFile(runWilcoxonRender.output("topTableFilteredCsv"), "csv", DEGImportParams, { mem: defaultConvMem, cpu: defaultConvCpu })

	// Create trace
	pAdjStr := string(pAdjThreshold)
	log2FCStr := string(log2FCThreshold)
	traceLabel := "DEG - Denominator: " + denominator + " (log2FC: " + log2FCStr + ", pAdj: " + pAdjStr + ")"
	trace := pSpec.makeTrace(inputSpec, { type: "milaboratories.sc-differential-expression", importance: 70, label: traceLabel })

	// Build exports with trace injection
	exports := {
		log2foldchange: {
			spec: trace.inject(DEGPf["log2foldchange.spec"]),
			data: DEGPf["log2foldchange.data"]
		},
		regulationDirection: {
			spec: trace.inject(DEGPf["regulationDirection.spec"]),
			data: DEGPf["regulationDirection.data"]
		}
	}
	
	return {
		outputs: {
			topTablePf: pframes.exportFrame(topTablePf),
			topTableFilteredPf: pframes.exportFrame(topTableFilteredPf),
			deErrors: pframes.exportFrame(deErrorsPf)
		},
		exports: exports
	}
})

